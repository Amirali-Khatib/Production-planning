!Stu. Name: Amirali Khatib
 Stu. Id: 9722743149;
Model:
sets:
period: y,z,s,u,v,i1,m,n,r,initial,a,profit,rate;
!y = choqondar kashteh shodeh dar sal t (TON) akhar
 z = qalat kharidari shodeh dar sal t (TON) akhar
 s = qalat forookhteh shodeh dar sal t (TON) akhar
 u = choqondar karidari shodeh dar sal t (TON) akhar
 v = choqondar forookhteh shodeh dar sal t (TON) akhar
 i1= tedad kargar ezafi dar sal t (nafar-saat) akhar
 m = sarmayeh gozari dar sal t (gav) ebteda
 n = tedad gosaleh nozad forookhteh shodeh sal t
 r = tedad goosaleh nozad madeh dar sal t
 initial = tedad nozad ha qabl az foroosh dar sal t
 a = mizan baz pardkht vam dar sal t ebteda
 profit = ...
 rate = ...
=====================================================;
land;
qalat_mt(land,period): x;
!x = qalat kashteh shode dar zamin i dar sal t (TON)
=====================================================;
year: age, p;
cow_mt(year,period): q;
!q = tedad gav j saleh dar sal t
!p = tedad avalieh gav ha dar sal 0
=====================================================;
endsets

data:
period = 1..5;
year = 1..12;
land = 1..4;
p = 10 10 10 10 10 10 10 10 10 10 10 0;
rate = 0.909 0.825 0.751 0.683 0.621;
enddata
max = @sum(period(t): rate(t) * profit(t));
!mahdoodiat nafar_saat;
@for(period(t): 10*(r(t)+q(1,t)) + 42*@sum(year(j)| j #LE# 10:q(j+1,t)) + 14*y(t)/1.5 + 4*(x(1,t)/1.1 + x(2,t)/0.9 + x(3,t)/0.8 + x(4,t)/0.65) - i1(t) <= 5500);
!mahdoodiat masahat zamin;
@for(period(t): (2/3)*(r(t)+q(1,t))+ @sum(year(j)| j #LE# 10:q(j+1,t)) + y(t)/1.5 + x(1,t)/1.1 + x(2,t)/0.9 + x(3,t)/0.8 + x(4,t)/0.65 <= 200);
!mahdoodiat gav;
r(1)+@sum(year(j)| j #LE# 10:q(j+1,1))-m(1) <= 130;
r(2)+@sum(year(j)| j #LE# 10:q(j+1,2))-m(1)-m(2) <= 130;
r(3)+@sum(year(j)| j #LE# 10:q(j+1,3))-m(1)-m(2)-m(3) <= 130;
r(4)+@sum(year(j)| j #LE# 10:q(j+1,4))-m(1)-m(2)-m(3)-m(4) <= 130;
r(5)+@sum(year(j)| j #LE# 10:q(j+1,5))-m(1)-m(2)-m(3)-m(4)-m(5) <= 130;

!mahdoodiat masahat qalat;
@for(period(t): x(1,t)/1.10 <= 20);
@for(period(t): x(2,t)/0.90 <= 30);
@for(period(t): x(3,t)/0.80 <= 20);
@for(period(t): x(4,t)/0.65 <= 10);

@for(period(t): r(t) = 0.5 * initial(t) - n(t) );
@for(period(t): initial(t) = 1.1 * @sum(year(j)|j #LE# 10:q(j+1,t)));

!mahdoodiat haye tavalod va marg;
q(1,1) = 0.95 * 10;
@for(period(t)|t #LE# 4: q(1,t+1) = 0.95 * r(t));

q(2,1) = 0.95 * p(1);
@for(period(t)|t #LE# 4: q(2,t+1) = 0.95 * q(1,t));

@for(year(j)|j #LE# 10: q(j+2,1) = 0.98 * p(j+1));
@for(year(j)|j #LE# 10: @for(period(t)|t #LE# 4:q(j+2,t+1) = 0.98 * q(j+1,t)));                                                                                                                                         

!qalat gav;
@for(period(t): @sum(land(i):x(i,t)) + z(t) - s(t) >= 0.6 * @sum(year(j)|j #LE# 10: q(j+1,t)));
!choqondar gav;
@for(period(t): y(t) + u(t) - v(t) >= 0.7 * @sum(year(j)|j #LE# 10: q(j+1,t)));

@sum(year(j)|j #LE# 10:q(j+1,5)) <= 175;
@sum(year(j)|j #LE# 10:q(j+1,5)) >= 50;

a(1) = 200 * 0.19925 * (m(1));
a(2) = 200 * 0.19925 * (m(1)+m(2));
a(3) = 200 * 0.19925 * (m(1)+m(2)+m(3));
a(4) = 200 * 0.19925 * (m(1)+m(2)+m(3)+m(4));
a(5) = 200 * 0.19925 * (m(1)+m(2)+m(3)+m(4)+m(5));

@for(period(t): profit(t) = (75*s(t) + 58*v(t) + 370*@sum(year(j)|j #LE# 10: q(j+1,t)) + 30*0.5*initial(t) + 40*n(t)+ 120*q(12,t)) - (90*z(t) + 70*u(t) + 4000 + 1.2*i1(t) + 50*(r(t)+q(1,t)) + 100*@sum(year(j)|j #LE# 10: q(j+1,t)) + 15*@sum(land(i):x(i,t)) + 10*y(t) + 200*m(t) + a(t)));